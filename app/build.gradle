apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt' // needed for Kotlin data binding functionality
apply plugin: 'kotlinx-serialization' // JSON serialization
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 31

    // Need the following for org.apache.commons:commons-text:1.8, and also apparently Kotlin support with Java 1.8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    defaultConfig {
        applicationId "rocks.underfoot.underfootandroid"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // Make sure all outputs include the name and the version name
    // https://www.tanelikorri.com/tutorial/android/set-apk-filename-in-gradle/
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Underfoot-${defaultConfig.versionName}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
    }

    namespace 'rocks.underfoot.underfootandroid'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Basics
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.android.material:material:1.5.0'
    implementation "androidx.activity:activity-ktx:1.5.0-alpha02"
    implementation "androidx.fragment:fragment-ktx:1.5.0-alpha02"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'

    // Backward compatibility
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'

    // Navigation
    // Java language implementation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"

    // Mapping
    implementation "com.mapzen.tangram:tangram:$tangram_version"

    // Slide-up panel. Can probably remove that when I remove the old activity
    repositories {
        mavenCentral()
    }
    implementation 'com.sothree.slidinguppanel:library:3.4.0'

    // Kotlinx serialization for JSON processing
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:1.0.0-RC" // JVM dependency

    implementation "net.lingala.zip4j:zip4j:2.6.2"
}
